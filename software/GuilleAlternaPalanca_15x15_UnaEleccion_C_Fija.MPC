\===============================================================Delmas,2016===\
\======================= Alternar Palancas 15 x 15 ===========================\
\ En este entrenamiento las ratas debe aprender que existen dos palancas que  \
\ entregan comida. El comienzo es al azar y luego de la mitas de los trials   \
\ cambia a la palanca opuesta. Existe dos palancas fijas, una Izquierda y otra\
\ Derecha con sus respectivos estimulos luminosos.                            \
\ Los palanqueos durante el ITI   NO son catigados con una penalizacion en tiempo, \
\ el ITI se extiende progresivamente.                                         \
\ UTILIZAR EN CAJAS 1 y 2                                                     \

\===============================================================Delmas,2016===\
\ Protocolo                                                                   \
\ Nº de trials = 30
\ Tiempo p\ palanqueo = 30min (def=10s)
\ Tiempo p\ comer     = 5s (def=5)
\ ITI                 = 5s (def=5) Luz apagada 

\=Variables===================================================================\
\ A=Trials
\ B=Respuestas
\ paramatros de los  trials                                                  \
^nTrials=30
^tPalanca=60       \definidos en comienzo de trial
\ C = ^pararComp=0 \Detiene el comparador de tiempo p/ palanqueo
\ Q=^ITI=0
\ P=^tComer=0
\ D = reloj segundos
\ E = lista
\ G = numero de palanqueos para FR
\ N = tasa del FR
\ H = array para la hora
\ I = Cuenta reloj
\ J = Reloj
\ K = Cuenta reloj
\ F = toma al azar
\ R = respuestas
\ S = palanqueos en ITI
\ Y = tiempo de trial
\ Z = Cantidad de Trials

dim H=2 \tiempo para el trials
dim J=2 \Reloj
dim D=1 \(0)Reloj segundos - (1)Tiempo de palanqueo
dim R=2 \Respuestas 1-Izq y 2-Der
dim S=2 \ (0)habilita control de ITI y cuenta palanqueos (1)IZQ y (2)DER

list E=0,1,2,3,4,5,6

\ Variables a guardar en disco en cada 'flush'.
DISKVARS=A,B,D,E(3),F,R,Y,Q,S,N
\=============================================================================\

\=SALIDAS=====================================================================\
\ ^constantes

 
\OUTPUT
^luzCaja=8 \\puertos
^refuerzo=2
^sacarPalancaShaping=16 \no se utiliza (centro)
^sacarPalancaDer=4
^sacarPalancaIzq=5
^luzPalancaDer=13 \luz cruz 
^luzPalancaIzq=14 \OFF luz cuadrado
^luzPalancaIzq2=12 \ON      cuadrado
^luzPalancaCen=15  
^luzComedero=3

\INPUT
^palancaDer=3
^palancaIzq=7
^palancaShaping=8 \ palancaCentro

\=============================================================================\
\ Z-PULSE en uso
\ Z1 = empieza el trial 
\ Z2 = Inicia comparacion, espera hasta q se cumpla el tiempo p/ palanquear
\      Luego se va a ITI
\ Z3 = Fin de trial (continua ITI sin luz)
\ Z4 = Inicio de ITI
\ Z5 = Control de luces de palanca
\ Z7 = Secuencia para dispenzar refuerzo
\ Z8 = Castigo por mala eleccion
\ Z9 = Habilita control de palanqueo en ITI
\=============================================================================\

S.S.1, \Control de la cantidad de experimentos y seteo de tiempos

S1,
    #START:Z1;set A=0;set B=0;set C=0; set R(0)=0;set R(1)=0;set Q=5";set Y=30;
           set P=5";set S(0)=0;set S(1)=0;set S(2)=0;set Z=16; set G=0;set N=3;set L=(Z/2) --->S2
S2,
  #Z1:add A;set C=0;set S(0)=0;set S(1)=0;set S(2)=0;set Q=5";
  show 1, Trial, A;show 2, Inicio,1; 
  if (A>Z) [@fin,@sigTrial]
                  @fin:--->S5
                  @sigTrial:if (A=(L+1)) [@cambia,@igual]
                                       @cambia: if (F>=E(3)) [@aMenor,@aMayor]
                                                         @aMenor: set F=1 --->S3
                                                         @aMayor: set F=5 --->S3 
                                       @igual: --->S3
  S3, \Enciende las luces estimulos
    0.01":OFF^sacarPalancaShaping;
          ON^sacarPalancaDer;    \NUNCA se GUARDAN
          ON^sacarPalancaIzq;
    Z5;
    time H(0),H(1),H(2); SET I=Y;
    SET I=(((H(0)*3600)+(H(1)*60)+H(2))+ I);Z2 ---> S4 
                              \ + tiempo de trial
  S4, \Apaga las luces estimulos
    #Z3:OFF^luzPalancaCen;
        OFF^luzComedero;
        OFF^luzPalancaDer;
        OFF^luzPalancaIzq;
        OFF^luzPalancaIzq2;
        OFF^sacarPalancaShaping;
        OFF^sacarPalancaDer;
        OFF^sacarPalancaIzq;
    show 2, finPalanqueo,1;Z4 ---> S2 \ espera fin de ITI  
  S5, 
    0.01":OFF^luzCaja;OFF^luzPalancaCen --->stopabortflush \Fin del experimentos

S.S.2,  \Reloj : contador de tiempo infinito
  S1,
    #START:--->S2
  S2,
    0.01":TIME  J(0),J(1),J(2); SET K=(J(0)*3600 + J(1)*60 + J(2));
    set D(0)=(I-K) --->S3
  S3,
    0.01":Show 3,t.trial,D(0) --->S2


S.S.3,  \Control de tiempo p/ palanquear, luego se va a ITI
  S1,
    #Z2: --->S2
  S2,        \ Reloj > tiempo p/ palanquear
    0.01": if (K > I) [@finTrial,@cont]
                        @finTrial:Z3 ---> S1 \ dispara apagar todo y luego ITI
                        @cont: --->S3
  S3,
    0.01": if (C>1) [@parar,@cont]
                              @parar: ---> S1  \Si para es xq se palanqueo
                              @cont: ---> S2

S.S.4,  \ITI
  S1,
    #Z4:set S(0)=1;show 2, ITI,D(0) ---> S2
  S2,
    Q#T:flush ---> S3  \ITI es el tiempo de ITI seteado en S.S.1
  S3,
    1.00":set S(0)=0;Z1 ---> S1


S.S.5, \Palanca Derecha Presionada 
  S1,
    #R^palancaDer:if(F>=E(3)) [@eligioBien,@eligioMAL]
                               @eligioBien:--->S3
                               @eligioMal: --->S2                              
  S2,
    0.01":if (C=1) [@nada,@cont]
                    @nada: ---> S5          \ Ya se palanqueo
                    @cont: ---> S1          \ No pasa nada, no hay contigencia
  S3,
    0.01":if (C=1) [@nada,@cont]
                    @nada: ---> S5          \ Ya se palanqueo
                    @cont: if G=G [@cont,@nada]   \ G=G pasa y no hay FR
                                   @cont: set G=0 ---> S4
                                   @nada: add G --->S1
  S4,
    0.01":set C=1;add R(1);Z7;show 6, ITI DER, S(2) --->S1 \ Se va a comer
  S5, \Incrementa el ITI = Q
    0.01":if(S(0)=1) [@incrementa,@nada]
                      @incrementa:set Q=Q+2";add S(2);show 2, ITI,Q --->S1
                      @nada: --->S1        

S.S.6, \Palanca Presionada Izquierda
  S1,
    #R^palancaIzq: if(F<E(3)) [@eligioBien,@eligioMAL]
                                @eligioBien:--->S3
                                @eligioMal: --->S2
  S2,
    0.01":if (C=1) [@nada,@cont]
                    @nada: ---> S5          \ Ya se palanqueo
                    @cont: ---> S1          \ No pasa nada, no hay contigencia
  S3,
    0.01":if (C=1) [@nada,@cont]
                    @nada: ---> S5          \ Ya se palanqueo
                    @cont: if G=G [@cont,@nada] \ G=G pasa y no hay FR
                                   @cont: set G=0 ---> S4
                                   @nada: add G --->S1          
  S4,
    0.01":set C=1;add R(0);Z7;show 5, ITI IZQ, S(1) --->S1 \ Se va a comer
  S5,    \Incrementa el ITI = Q
    0.01":if(S(0)=1) [@incrementa,@nada]
                      @incrementa:set Q=Q+2";add S(1);show 2, ITI,Q --->S1
                      @nada: --->S1 


S.S.7, \ Secuencia para dar refuerzo
  S1,
    #Z7:set D(1)=D(0) ---> S2
  S2,
    0.1":OFF^luzPalancaCen;
         ON^luzComedero;
         OFF^luzPalancaDer;
         OFF^luzPalancaIzq;
         OFF^luzPalancaIzq2;
    show 2, COMER,D(0);
    show 4, Respuestas, R(0);show 5, Respuestas, R(1)  --->S3
  S3,
    0.2":ON^refuerzo ---> S4  \Ver si es necesario sonido
  S4,
    0.01":OFF^refuerzo ---> S5
  S5,
    0.01": --->S6
  S6,
    P#T:Z3 --->S1  \Se va a apagar todo y al ITI
		

\S.S.8, \ castigo por mala eleccion TIEMPO
\  S1,
\    #Z8:set D(1)=D(0) ---> S2
\  S2,
\    0.1":OFF^luzPalancaCen;
\         OFF^luzComedero;
\         OFF^luzPalancaDer;
\         OFF^luzPalancaIzq;
\    show 4, MALA ELECCION, R(0);show 5, MALA ELECCION, R(1); --->S3
\  S3,
\    3.00":Z3 --->S1  \Se va a apagar todo y al ITI

		
S.S.9, \Control de luces de palancas
  S1, 
    #Z5: if(F>=E(3)) [@luzDer,@luzIzq]
                      @luzDer: --->S2
                      @luzIzq: --->S3 
  S2,
    0.01":ON^luzPalancaDer ---> S1
  S3,
    0.01":OFF^luzPalancaIzq; ON^luzPalancaIzq2 ---> S1  


